library(
    identifier: 'pipeline-lib@4.3.6',
    retriever: modernSCM([$class: 'GitSCMSource',
                          remote: 'https://github.com/SmartColumbusOS/pipeline-lib',
                          credentialsId: 'jenkins-github-user'])
)

properties([
    pipelineTriggers([scos.dailyBuildTrigger()]),
])

def image, smokeTestImage
def doStageIf = scos.&doStageIf
def doStageIfRelease = doStageIf.curry(scos.changeset.isRelease)
def doStageUnlessRelease = doStageIf.curry(!scos.changeset.isRelease)
def doStageIfPromoted = doStageIf.curry(scos.changeset.isMaster)

node ('infrastructure') {
    ansiColor('xterm') {
        scos.doCheckoutStage()

        doStageUnlessRelease('Build') {
            withCredentials([string(credentialsId: 'hex-read', variable: 'HEX_TOKEN')]) {
                image = docker.build("scos/discovery-streams:${env.GIT_COMMIT_HASH}", '--build-arg HEX_TOKEN=$HEX_TOKEN .')

                sh('''
                export HOST_IP=$(curl http://169.254.169.254/latest/meta-data/local-ipv4)
                mix local.hex --force
                mix local.rebar --force
                mix hex.organization auth smartcolumbus_os --key $HEX_TOKEN
                mix deps.get
                mix test.integration
                ''')
            }

            dir('smoke-test') {
                smokeTestImage = docker.build("scos/discovery-streams-smoke-test:${env.GIT_COMMIT_HASH}")
            }
        }

        doStageUnlessRelease('Deploy to Dev') {
            scos.withDockerRegistry {
                image.push()
                smokeTestImage.push()
                image.push('latest')
                smokeTestImage.push('latest')
            }
            deployConsumerTo(environment: 'dev')
        }

        doStageIfPromoted('Deploy to Staging')  {
            def environment = 'staging'

            deployConsumerTo(environment: 'staging')

            scos.applyAndPushGitHubTag(environment)

            scos.withDockerRegistry {
                image.push(environment)
                smokeTestImage.push(environment)
            }
        }

        doStageIfRelease('Deploy to Production') {
            def releaseTag = env.BRANCH_NAME
            def promotionTag = 'prod'

            deployConsumerTo(environment: 'prod', internal: false)

            scos.applyAndPushGitHubTag(promotionTag)

            scos.withDockerRegistry {
                image = scos.pullImageFromDockerRegistry("scos/discovery-streams", env.GIT_COMMIT_HASH)
                smokeTestImage = scos.pullImageFromDockerRegistry("scos/discovery-streams-smoke-test", env.GIT_COMMIT_HASH)
                image.push(releaseTag)
                smokeTestImage.push(releaseTag)
                image.push(promotionTag)
                smokeTestImage.push(promotionTag)
            }
        }
    }
}

def deployConsumerTo(params = [:]) {
    def environment = params.get('environment')
    if (environment == null) throw new IllegalArgumentException("environment must be specified")

    def internal = params.get('internal', true)

    scos.withEksCredentials(environment) {

        def terraformOutputs = scos.terraformOutput(environment)
        def subnets = terraformOutputs.public_subnets.value.join(/\\,/)
        def allowInboundTrafficSG = terraformOutputs.allow_all_security_group.value
        def certificateARN = internal ? terraformOutputs.tls_certificate_arn.value : terraformOutputs.root_tls_certificate_arn.value
        def ingressScheme = internal ? 'internal' : 'internet-facing'

        sh("""#!/bin/bash
            set -e
            helm init --client-only
            helm upgrade --install discovery-streams ./chart \
                --namespace=discovery \
                --set ingress.enabled="true" \
                --set ingress.scheme="${ingressScheme}" \
                --set ingress.subnets="${subnets}" \
                --set ingress.securityGroups="${allowInboundTrafficSG}" \
                --set ingress.dnsZone="${environment}.internal.smartcolumbusos.com" \
                --set ingress.certificateARN="${certificateARN}" \
                --set image.tag="${env.GIT_COMMIT_HASH}"
        """.trim())
    }
}
