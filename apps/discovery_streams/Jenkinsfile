library(
    identifier: 'pipeline-lib@4.3.6',
    retriever: modernSCM([$class: 'GitSCMSource',
                          remote: 'https://github.com/SmartColumbusOS/pipeline-lib',
                          credentialsId: 'jenkins-github-user'])
)

properties([
    pipelineTriggers([scos.dailyBuildTrigger()]),
])

def image, smokeTestImage
def doStageIf = scos.&doStageIf
def doStageIfRelease = doStageIf.curry(scos.changeset.isRelease)
def doStageUnlessRelease = doStageIf.curry(!scos.changeset.isRelease)
def doStageIfPromoted = doStageIf.curry(scos.changeset.isMaster)

node ('infrastructure') {
    ansiColor('xterm') {
        scos.doCheckoutStage()

        doStageUnlessRelease('Build') {
            image = docker.build("scos/cota-streaming-consumer:${env.GIT_COMMIT_HASH}")
            dir('smoke-test') {
                smokeTestImage = docker.build("scos/cota-consumer-smoke-test:${env.GIT_COMMIT_HASH}")
            }
        }

        doStageUnlessRelease('Deploy to Dev') {
            scos.withDockerRegistry {
                image.push()
                smokeTestImage.push()
                image.push('latest')
                smokeTestImage.push('latest')
            }
            deployConsumerTo(environment: 'dev')
        }

        doStageIfPromoted('Deploy to Staging')  {
            def promotionTag = scos.releaseCandidateNumber()

            deployConsumerTo(environment: 'staging')

            scos.applyAndPushGitHubTag(promotionTag)

            scos.withDockerRegistry {
                image.push(promotionTag)
                smokeTestImage.push(promotionTag)
            }
        }

        doStageIfRelease('Deploy to Production') {
            def releaseTag = env.BRANCH_NAME
            def promotionTag = 'prod'

            deployConsumerTo(environment: 'prod', internal: false)

            scos.applyAndPushGitHubTag(promotionTag)

            scos.withDockerRegistry {
                image = scos.pullImageFromDockerRegistry("scos/cota-streaming-consumer", env.GIT_COMMIT_HASH)
                smokeTestImage = scos.pullImageFromDockerRegistry("scos/cota-consumer-smoke-test", env.GIT_COMMIT_HASH)
                image.push(releaseTag)
                smokeTestImage.push(releaseTag)
                image.push(promotionTag)
                smokeTestImage.push(promotionTag)
            }
        }
    }
}

def deployConsumerTo(params = [:]) {
    def environment = params.get('environment')
    if (environment == null) throw new IllegalArgumentException("environment must be specified")

    def internal = params.get('internal', true)

    scos.withEksCredentials(environment) {

        def terraformOutputs = scos.terraformOutput(environment)
        def subnets = terraformOutputs.public_subnets.value.join(/\\,/)
        def allowInboundTrafficSG = terraformOutputs.allow_all_security_group.value
        def certificateARN = scos.terraformOutput(environment, internal ? 'operating-system' : 'prod').tls_certificate_arn.value
        def ingressScheme = internal ? 'internal' : 'internet-facing'

        sh("""#!/bin/bash
            set -e
            helm init --client-only
            helm upgrade --install cota-streaming-consumer ./chart \
                --namespace=cota-services \
                --set ingress.enabled="true" \
                --set ingress.scheme="${ingressScheme}" \
                --set ingress.subnets="${subnets}" \
                --set ingress.securityGroups="${allowInboundTrafficSG}" \
                --set ingress.dnsZone="${environment}.internal.smartcolumbusos.com" \
                --set ingress.certificateARN="${certificateARN}" \
                --set image.tag="${env.GIT_COMMIT_HASH}"
        """.trim())
    }
}
